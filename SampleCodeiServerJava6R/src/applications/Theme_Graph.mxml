<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:ic="http://www.supermap.com/iclient/2010"
			   xmlns:isj6="http://www.supermap.com/iserverjava/2010"
			   width="100%" height="100%"
			   creationComplete="initApp()">
	<!--统计专题图-->
	
	<fx:Script>
		<![CDATA[			
			import com.supermap.web.core.Rectangle2D;
			import com.supermap.web.utils.serverTypes.ServerColor;
			import com.supermap.web.utils.serverTypes.ServerStyle;
			import com.supermap.web.iServerJava6R.themeServices.*;
			
			import mx.rpc.*;
			import mx.rpc.events.FaultEvent;
			
			import utilEvent.ReadURLEvent;
			
			private var isTheme:Boolean = false;//判断是否已制作专题图
			private var layerid:String;
			private var themeLayer:TiledDynamicRESTLayer;
			private var viewConfig:ViewConfig;
			[Bindable]
			private var mapUrl:String;
			private function initApp():void
			{
				viewConfig = new ViewConfig(this);
				this.addEventListener(ReadURLEvent.READ_COMPLETE, readURLCompelte);
			} 
			
			//获取服务地址
			private function readURLCompelte(event:ReadURLEvent):void
			{
				this.map.viewBounds = new Rectangle2D(115, 38.5, 119, 41);
				//获取地图服务地址，默认 IP 为：localhost
				//用户若要更改服务地址，直接修改服务地址配置文件 mapUrlConfig.xml
				//或直接在此输入地图服务地址
				//格式如：restUrl = http://localhost:8090/iserver/services/map-world/rest/maps/World Map
				mapUrl = viewConfig.webUrl + ViewConfig.JingJin_MAP;
			}
			
			//定义统计专题图子项
			private function setItems():Array
			{
				var item1:ThemeGraphItem = new ThemeGraphItem();
				item1.caption = "1992-1995人口增长率";
				item1.graphExpression = "Pop_Rate95";
				var style1:ServerStyle = new ServerStyle();
				style1.fillForeColor = new ServerColor();
				style1.fillForeColor.blue = 234;
				style1.fillForeColor.green = 73;
				style1.fillForeColor.red = 92;
				item1.uniformStyle = style1;
				
				var item2:ThemeGraphItem = new ThemeGraphItem();
				item2.caption = "1995-1999人口增长率";
				item2.graphExpression = "Pop_Rate99";
				var style2:ServerStyle = new ServerStyle();
				style2.fillForeColor = new ServerColor();
				style2.fillForeColor.blue = 240;
				style2.fillForeColor.green = 111;
				style2.fillForeColor.red = 211;
				item2.uniformStyle = style2;
				
				var items:Array = [item1, item2];
				return items;
			}
 
			protected function submitHandler():void
			{ 
				if(this.isTheme == true)
					return;
				//定义统计专题图
				var theme:ThemeGraph = new ThemeGraph();	
				theme.items = this.setItems();
				theme.barWidth = 0.05;
				theme.graduatedMode = GraduatedMode.SQUAREROOT;
				theme.graphAxes.axesDisplayed = true; 
				theme.graphSize.maxGraphSize = 1;
				theme.graphSize.minGraphSize = 0.35;
				theme.graphText.graphTextDisplayed = true;
				theme.graphText.graphTextFormat = ThemeGraphTextFormat.VALUE;
				theme.graphType = ThemeGraphType.BAR3D;
				
				//定义获取专题图时所需参数
				var themeParam:ThemeParameters = new ThemeParameters();//专题图参数对象 
				themeParam.themes = [theme];				
				themeParam.dataSourceNames = ["Jingjin"];
				themeParam.datasetNames =["BaseMap_R"];
				
				//与服务端交互
				var themeservice:ThemeService = new ThemeService(mapUrl);
				themeservice.processAsync(themeParam,new AsyncResponder(this.displayTheme, excuteThemeErros, null));
				
			}
			
			//专题图获取成功时调用的处理函数
			private function displayTheme(themeresult:ThemeResult, mark:Object = null):void
			{
				this.isTheme = true;
				themeLayer = new TiledDynamicRESTLayer();
				themeLayer.url = mapUrl;				
				themeLayer.transparent = true;
				themeLayer.layersID = themeresult.resourceInfo.newResourceID;	
				layerid = themeresult.resourceInfo.newResourceID;
				this.map.addLayer(themeLayer);
			}
			
			//删除专题图
			private function deleteHandler():void
			{
				var themeRemove:RemoveThemeService = new RemoveThemeService(mapUrl);	 
				var themeRemoveParam:RemoveThemeParameters = new RemoveThemeParameters();
				themeRemoveParam.id = layerid; 
				themeRemove.processAsync(themeRemoveParam,new AsyncResponder(this.delTheme, excuteThemeErros, null));
			}
			
			//专题图删除成功时调用的处理函数
			private function delTheme(themeResult:RemoveThemeResult,mark:Object = null):void
			{
				this.isTheme = false;
				if(themeResult.succeed = "true" && themeLayer)
				{
					this.map.removeLayer(themeLayer);
				}
			}
			
			//与服务端交互失败时调用 的处理函数
			private function excuteThemeErros(event:FaultEvent, mark:Object = null):void
			{
			}
		]]>
	</fx:Script>
	
	<!--加载地图-->
	<s:Group width="100%" height="100%" >	
		<ic:Map id="map" x="0" y="0"  height="100%" width="100%" zoomDuration="500">
			<ic:TiledDynamicRESTLayer url="{mapUrl}" enableServerCaching="false"/>
		</ic:Map>
	</s:Group>
	
	<!--操作窗口-->
	<s:Panel id="titlewin" title="统计专题图" fontFamily="宋体" fontSize="12" right="5" top="10" 
			 backgroundColor="#454343" backgroundAlpha="0.48" height="108">
		<s:VGroup top="5" gap="10" horizontalCenter="0" bottom="10">  
			<mx:Text text="人口增率统计专题图"/>
			<s:HGroup horizontalAlign="center" width="100%">
				<s:Button id="addButton" label="生成专题图" click="submitHandler()"/>
				<mx:Spacer width="10"/>
				<s:Button id="deleteButton" label="移除专题图" click="deleteHandler()" x="100"/>
			</s:HGroup>
		</s:VGroup>
	</s:Panel> 
</s:Application>
