<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:ic="http://www.supermap.com/iclient/2010"
			   xmlns:is="http://www.supermap.com/iserverjava/2010"
			   width="100%" height="100%"
			   creationComplete="initApp()">
	<!--SQL查询-->
	<fx:Script>
		<![CDATA[
			import com.supermap.web.actions.Pan;
			import com.supermap.web.components.GraphicDataGrid;
			import com.supermap.web.core.Feature;
			import com.supermap.web.core.Graphic;
			import com.supermap.web.core.styles.PredefinedFillStyle;
			import com.supermap.web.core.styles.graphicStyles.GraphicFillStyle;
			import com.supermap.web.core.styles.graphicStyles.GraphicMarkerStyle;
			import com.supermap.web.events.GraphicDataGridEvent;
			import com.supermap.web.iServerJava6R.FilterParameter;
			import com.supermap.web.iServerJava6R.Recordset;
			import com.supermap.web.iServerJava6R.queryServices.*;
			import com.supermap.web.utils.serverTypes.*;
			import com.supermap.web.iServerJava6R.spatialAnalystServices.*;
			import com.supermap.web.mapping.HighlightLayer;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.ListEvent;
			import mx.rpc.AsyncResponder;
			
			import spark.events.IndexChangeEvent;
			
			import utilEvent.ReadURLEvent;
			
			[Bindable]
			private var queryLayers:Array = ["Countries@World"];//查询图层列表  
			private var highlayer:HighlightLayer;//高亮图层				
			private var recordGrid:GraphicDataGrid;
			private var currentQueryLayer:String = "Countries@World";//当前查询图层
			private var viewConfig:ViewConfig;
			[Bindable]
			private var mapUrl:String;
			private var resultFeatures:Array = [];
			
			private function initApp():void
			{
				viewConfig = new ViewConfig(this);
				this.addEventListener(ReadURLEvent.READ_COMPLETE, readURLCompelte);
			} 
			
			//获取服务地址
			private function readURLCompelte(event:ReadURLEvent):void
			{
				//获取地图服务地址，默认 IP 为：localhost
				//用户若要更改服务地址，直接修改服务地址配置文件 mapUrlConfig.xml
				//或直接在此输入服务地址
				//格式如：restUrl = http://localhost:8090/iserver/services/map-world/rest/maps/World Map
				mapUrl = viewConfig.webUrl + ViewConfig.World_MAP1;
				
				highlayer = new HighlightLayer(mapUrl);
			}
			
			//查询
			private function onExcuteQueryClick(event:MouseEvent):void 
			{
				//定义 SQL 查询参数
				var queryBySQLParam:QueryBySQLParameters = new QueryBySQLParameters();
				var filter:FilterParameter = new FilterParameter();
				filter.name = this.querylayer.selectedItem.toString();
				filter.attributeFilter = this.txtSqlExpress.text;
				queryBySQLParam.filterParameters = [filter];
				queryBySQLParam.returnContent = this.ishighLightLayer.selectedIndex == 0;
				
				/* 执行 SQL 查询 */
				var queryByDistanceService:QueryBySQLService = new QueryBySQLService(mapUrl);
				queryByDistanceService.processAsync(queryBySQLParam, new AsyncResponder(this.displayQueryRecords, 
					function (object:Object, mark:Object = null):void
					{
						Alert.show("与服务端交互失败", "抱歉", 4, this);
					}, null));
				
			}	 
			
			//显示查询结果
			private function displayQueryRecords(queryResult:QueryResult, mark:Object = null):void
			{
				
				if(this.ishighLightLayer.selectedIndex == 0)
				{
					if(!bQueryResultSaved.selected)
					{
						resultFeatures = [];
						if(this.recordGrid)
						{
							this.recordGrid.clear();
						}
						this.graphicsLayer.removeAll();
					}
					if(queryResult.recordsets == null || queryResult.recordsets.length == 0)
					{
						Alert.show("查询结果为空", null, 4, this);
						return;
					}
					var recordSets:Array = queryResult.recordsets;
					if(recordSets.length != 0)
					{
						for each(var recordSet:Recordset in recordSets)
						{
							if(!recordSet.features || recordSet.features.length == 0)
							{
								Alert.show("当前图层查询结果为空", null, 4, this);
								return;
							}
							outerLoop:
							for each (var feature:Feature in recordSet.features)
							{
								for each(var value:Feature in resultFeatures)
								{
									if(value.attributes.SMID == feature.attributes.SMID)
									{
										break outerLoop;
									}
								}
								resultFeatures.push(feature);
							}
						}
					}
					//转换feature为graphic
					var array:Array = [];
					for each (var fea:Feature in resultFeatures)
					{
						var graphicFillStyle:GraphicFillStyle = new GraphicFillStyle();
						var graphic:Graphic=new Graphic(fea.geometry,graphicFillStyle);
						graphic.attributes = fea.attributes;
						array.push(graphic);
					}
					this.highlayer.visible = false;
					
					this.recordGrid = new GraphicDataGrid(this.graphicsLayer, array, this.result); 
					this.recordGrid.fields = ["SmID","CAPITAL","CONTINENT","COUNTRY","SmArea"];
					this.recordGrid.captions =["SmID","首都","所属洲","国家","面积"];
					this.recordGrid.left = 5;
					this.recordGrid.right = 5;
					this.recordGrid.bottom = 5;
					this.recordGrid.top = 2;
					
					this.result.removeAllChildren();
					this.recordGrid.addEventListener(GraphicDataGridEvent.GRAPHIC_SELECTED, FDSeledHandler);
					this.recordGrid.highLightStyle =  new GraphicFillStyle("solid",0x4272d7,1);
					this.result.addChild(this.recordGrid); 
					this.result.visible = true; 
				}
					//使用高亮图层 HighlightLayer 显示查询结果
				else
				{
					if(this.recordGrid)
					{
						this.recordGrid.clear();
					}
					this.graphicsLayer.removeAll();
					this.result.visible = false;
					if (queryResult.resourceInfo!= null)
					{
						highlayer.visible = true;
						highlayer.queryResultID = queryResult.resourceInfo.newResourceID;	
						var serverstyle:ServerStyle = new ServerStyle();
						serverstyle.lineWidth = 0.5;
						serverstyle.fillOpaqueRate = 80;
						highlayer.style = serverstyle;
						this.map.addLayer(highlayer);
					}
				}
			}
			
			private function FDSeledHandler(event:GraphicDataGridEvent) : void
			{
				map.panTo(event.graphic.geometry.bounds.center);
			}
			
			//查询图层改变事件 
			private function onQueryLayerChange(event:ListEvent):void
			{
				graphicsLayer.removeAll(); 	
				var queryLayer:String = ComboBox(event.target).selectedItem.toString();
				currentQueryLayer = queryLayer;
			}
			
			//平移
			private function pan(event:MouseEvent):void
			{
				map.action = new Pan(map); 
			}
			
			//清除查询结果
			private function clearFeature(event:MouseEvent):void
			{
				if(highlayer && highlayer.numChildren > 0)
				{
					this.map.removeLayer(highlayer);
				}
				
				if(this.recordGrid)
				{
					this.recordGrid.clear();
				}
				this.graphicsLayer.removeAll();
				this.result.removeAllChildren();
				this.result.visible = false;
			}
			
			//查询参数设置窗口关闭事件
			private function titleWinClose(event:CloseEvent):void 
			{ 
				this.result.visible = false;
			}
			
			private function resultWin_mouseUp(event:MouseEvent):void
			{
				result.stopDrag();
			}
			
			private function resultWin_mouseDown(event:MouseEvent):void
			{
				result.startDrag();
			}
			
			
			protected function ishighLightLayer_changeHandler(event:IndexChangeEvent):void
			{
				if(this.ishighLightLayer.selectedIndex == 1)
				{
					this.bQueryResultSaved.enabled = false;
					this.bQueryResultSaved.selected = false;
				}
				else
					this.bQueryResultSaved.enabled = true;
			}
			
		]]>
	</fx:Script>
	
	<!--添加地图-->
	<s:Panel fontFamily="宋体" width="100%" height="100%" fontSize="18" title="操作指南：直接点击“查询”">
		<ic:Map id="map" scales="{[1.25e-9, 2.5e-9, 5e-9, 1e-8, 2e-8, 4e-8, 8e-8, 1.6e-7, 3.205e-7, 6.4e-7]}">
			
			<ic:TiledDynamicRESTLayer url="{this.mapUrl}"/>
			<ic:GraphicsLayer id="graphicsLayer" />		 
		</ic:Map>
	</s:Panel>
	
	<!--定义结果显示窗口-->
	<mx:TitleWindow id="result" right="50" left="50" height="150" title="查询结果：" visible="false" mouseDown="resultWin_mouseDown(event)" layout="absolute" showCloseButton="true" 
					bottom="10" horizontalCenter="10" close="titleWinClose(event)" backgroundColor="#737171" backgroundAlpha="0.60" mouseUp="resultWin_mouseUp(event)">
		
	</mx:TitleWindow>
	
	<!--查询参数设置窗口-->
	<s:Panel id="titlewin" title="SQL 查询：" fontFamily="宋体" fontSize="12" right="5" top="10" backgroundColor="#454343" backgroundAlpha="0.48">
		<s:VGroup gap="10" left="5" top="5" bottom="5" right="5">
			<s:HGroup>
				<mx:Label text="查询图层："/>
				<mx:Spacer width="6"/>
				<mx:ComboBox  id="querylayer" dataProvider="{queryLayers}" change="onQueryLayerChange(event)" width="160"/>
			</s:HGroup>
			<s:HGroup>
				<mx:Label text="SQL 语句："/>
				<mx:Spacer width="3"/>
				<mx:TextInput id="txtSqlExpress" text="smid=247"/>
			</s:HGroup>
			<s:HGroup gap="30" horizontalAlign="center" width="100%" height="100%">
				<s:DropDownList id="ishighLightLayer"
								horizontalCenter="0"
								fontSize="12"
								fontFamily="宋体"
								selectedIndex="0" width="162" change="ishighLightLayer_changeHandler(event)">
					<s:ArrayList>
						<fx:String>使用高性能矢量渲染图层显示结果</fx:String>
						<fx:String>使用高亮图层显示结果</fx:String>
					</s:ArrayList>
				</s:DropDownList>
			</s:HGroup>
			<s:HGroup gap="30" horizontalAlign="center" width="100%" height="100%">
				<s:CheckBox id="bQueryResultSaved" label="保留查询结果" selected="false"/>
			</s:HGroup>
			<s:HGroup gap="10" width="100%" height="100%" horizontalAlign="center">
				<mx:Button label="查询" id="excuteQuery" click="onExcuteQueryClick(event)"/>
				<mx:Button label="清除" id="clear" click="clearFeature(event)"/>
				<mx:Button label="平移" id="panMap" click="pan(event)"/>
			</s:HGroup>
		</s:VGroup>
	</s:Panel>
</s:Application>
